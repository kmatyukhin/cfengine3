bundle common ftp_server
{
  classes:
      "ftp_server" or => { 'netrino' };
}

bundle agent ftp_server_autorun
{
  meta:
      "tags" slist => { "autorun" };

  methods:
      "ftp_server"
        usebundle => ftp_server_setup,
        ifvarclass => "$(ftp_server.ftp_server)";
}

bundle agent ftp_server_setup
{
  vars:
      "vsftpd_conf[listen]"             string => "YES";
      "vsftpd_conf[write_enable]"       string => "YES";
      "vsftpd_conf[xferlog_std_format]" string => "NO";
      "vsftpd_conf[chroot_local_user]"  string => "YES";
      "vsftpd_conf[utf8_filesystem]"    string => "YES";

  files:
      "/etc/vsftpd.conf"
        create => "false",
        edit_line => set_line_based("$(this.bundle).vsftpd_conf", "=", "\s*=\s*", ".*", "\s*#\s*"),
        classes => if_repaired("vsftpd_reload");
      "/var/list/."
        create => "true",
        perms => mog("500", "list", "list");
      "/var/list/upload/."
        create => "true",
        perms => mog("700", "list", "list");

  packages:
      "vsftpd"
        package_policy => "addupdate";

  processes:
      "vsftpd"
        restart_class => "vsftpd_reload";

  commands:
    vsftpd_reload::
      "/bin/systemctl reload vsftpd"
        contain => silent;
}
