bundle common ipip6_tunnel
{
  vars:
      "config" data => parsejson('{
        "server": {
          "class": "netrino",
          "ip6": "2a01:7e01::f03c:91ff:fe68:a882",
          "ip4": "10.0.3.1"
        },
        "client": {
          "class": "raspberrypi",
          "ip6": "2a01:c50e:3620:8300:380a:25d4:bfee:3f7",
          "ip4": "10.0.3.2",
          "subnet": "192.168.133.0/24"
        }
      }');

  classes:
      "ipip6_tunnel_configured"
        expression => returnszero("/sbin/ip -6 tunnel list sit1", "noshell");
}

bundle agent ipip6_tunnel_autorun
{
  #meta:
  #    "tags" slist => { "autorun" };

  methods:
    "$(ipip6_tunnel.config[client][class])"::
      "client"
        usebundle => ipip6_tunnel_client;
    "$(ipip6_tunnel.config[server][class])"::
      "server"
        usebundle => ipip6_tunnel_server;
}

bundle agent ipip6_tunnel_client
{
  commands:
      "/bin/ping -c1 -w1"
        args => "$(ipip6_tunnel.config[server][ip4])",
        contain => silent,
        classes => ipip6_tunnel_class;

    !ipip6_tunnel_configured::
      "/sbin/ip -6 tunnel add"
        arglist => { "sit1 mode ipip6",
                     "local $(ipip6_tunnel.config[client][ip6])",
                     "remote $(ipip6_tunnel.config[server][ip6])" },
        contain => silent;
      "/sbin/ip address add"
        arglist => { "$(ipip6_tunnel.config[client][ip4])",
                     "peer $(ipip6_tunnel.config[server][ip4])",
                     "dev sit1" },
        contain => silent;
      "/sbin/ip link set"
        args    => "sit1 up",
        contain => silent;
      "/sbin/ip route add"
        arglist => { "default",
                     "via $(ipip6_tunnel.config[server][ip4])",
                     "dev sit1",
                     "src 192.168.133.1" },
        contain => silent;
      "/sbin/ip route del"
        arglist => { "default",
                     "via 192.168.1.1" },
        contain => silent;

  reports:
    !ipip6_tunnel_up::
      "ipip6 tunnel is down";
}

bundle agent ipip6_tunnel_server
{
  commands:
    !ipip6_tunnel_configured::
      "/sbin/ip -6 tunnel add"
        arglist => { "sit1 mode ipip6",
                     "local $(ipip6_tunnel.config[server][ip6])",
                     "remote $(ipip6_tunnel.config[client][ip6])" },
        contain => silent;
      "/sbin/ip address add"
        arglist => { "$(ipip6_tunnel.config[server][ip4])",
                     "peer $(ipip6_tunnel.config[client][ip4])",
                     "dev sit1" },
        contain => silent;
      "/sbin/ip link set"
        args    =>   "sit1 up",
        contain => silent;
      "/sbin/ip route add"
        arglist => { "$(ipip6_tunnel.config[client][subnet])",
                     "via $(ipip6_tunnel.config[client][ip4])",
                     "dev sit1" },
        contain => silent;
}

body classes ipip6_tunnel_class
{
  promise_repaired => { "ipip6_tunnel_up" };
}
