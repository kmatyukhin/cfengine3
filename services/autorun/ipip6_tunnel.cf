bundle common ipip6_tunnel
{
  vars:
      "config" data => parsejson('{
        "server": {
          "class": "netrino",
          "ip6": "2a01:7e01::f03c:91ff:fe68:a882",
          "ip4": "10.0.3.1"
        },
        "client": {
          "class": "raspberrypi",
          "ip6": "2a01:c50e:3620:8300:33a5:828b:bad7:31c",
          "ip4": "10.0.3.2"
        }
      }');
}

bundle agent ipip6_tunnel_autorun
{
  meta:
      "tags" slist => { "autorun" };

  methods:
    "$(ipip6_tunnel.config[client][class])"::
      "client"
        usebundle => ipip6_tunnel_client;
    "$(ipip6_tunnel.config[server][class])"::
      "server"
        usebundle => ipip6_tunnel_server;
}

bundle agent ipip6_tunnel_client
{
  commands:
      "/bin/ping -c1 -w1"
        args => "$(ipip6_tunnel.config[server][ip4])",
        contain => silent,
        classes => ipip6_tunnel_class;

  reports:
    ipip6_tunnel_up::
      "ipip6 tunnel is up";
}

bundle agent ipip6_tunnel_server
{
  classes:
      "ipip6_tunnel_configured"
        expression => returnszero("/sbin/ip -6 tun list sit1", "noshell");

  commands:
    !ipip6_tunnel_configured::
      "/sbin/ip -6 tun add sit1 mode ipip6"
        arglist => { "local $(ipip6_tunnel.config[server][ip6])",
                     "remote $(ipip6_tunnel.config[client][ip6])" },
        contain => silent;
}

body classes ipip6_tunnel_class
{
  promise_repaired => { "ipip6_tunnel_up" };
}
